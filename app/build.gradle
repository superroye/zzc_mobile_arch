import java.util.function.Function
import java.util.function.Predicate
import java.util.stream.Collectors

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        applicationId "com.wolf.bestarch"
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    def src_dir = new File(projectDir, 'src')
// 扫描当前模块下的Pins模块，并生成List
    def p_module_names =
            src_dir
                    .list()
                    .toList()
                    .stream()
                    .filter(
                    new Predicate<String>() {
                        @Override
                        boolean test(String name) {
                            return name == 'main' || (name.startsWith('p_') && new File(src_dir, name).isDirectory())
                        }
                    })
                    .collect(Collectors.toList())
// 把生成的List合成为srcDir格式
    def p_src_dirs =
            p_module_names
                    .stream()
                    .map(
                    new Function() {
                        @Override
                        Object apply(Object module) {
                            return ['src', module, 'java'].join('/')
                        }
                    })
                    .collect(Collectors.toList())

    def p_res_dirs =
            p_module_names
                    .stream()
                    .map(
                    new Function() {
                        @Override
                        Object apply(Object module) {
                            return ['src', module, 'res'].join('/')
                        }
                    })
                    .collect(Collectors.toList())
// 指定路径
    sourceSets {
        main {
            manifest.srcFile "src/main/AndroidManifest.xml"
            java.srcDirs = p_src_dirs
            res.srcDirs = p_res_dirs
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {

        //use java 1.8 with Android Studio 3.0+
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api project(':base')

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
